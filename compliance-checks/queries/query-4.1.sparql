PREFIX daont: <http://www.semanticweb.org/daont#>
PREFIX dpv: <https://w3c.github.io/dpv/2.2/dpv/#>

SELECT DISTINCT 
    ?b2cSharing 
    ?user 
    ?holder 
    ?product
    ?data 
    ("DATA_NOT_PROVIDED" AS ?violationType)
    ("Article 4(1) VIOLATED: Data holder failed to provide data to user" AS ?details)
WHERE {
  ?b2cSharing a daont:B2CDataSharing ;
              daont:governedBy ?contract .
  ?contract dpv:hasRecipient ?user .
  ?user a daont:ConsumerUser ;
        daont:ownsOrUses ?product ;
        daont:requestsAccessTo ?data .
  ?product a daont:ConnectedProduct .
  ?data daont:isGeneratedBy ?product .
  ?holder a daont:DataHolder ;
          dpv:hasData ?data .
  
  FILTER NOT EXISTS {
    ?holder daont:performsLegalAction ?provision .
    ?provision a daont:ProvideData .
  }
}
ORDER BY ?user


#### OUTPUTS:

#b2cSharing || user || holder || product || data || violationType || details
#http://www.semanticweb.org/daont#b2cSharing_charlie || http://www.semanticweb.org/daont#charlie ||	http://www.semanticweb.org/daont#watchManufacturer ||	http://www.semanticweb.org/daont#smartWatch1	|| http://www.semanticweb.org/daont#charlieHealthData	|| "DATA_NOT_PROVIDED"	|| "Article 4(1) VIOLATED: Data holder failed to provide data to user"