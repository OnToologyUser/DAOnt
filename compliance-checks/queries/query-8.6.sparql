PREFIX daont: <http://www.semanticweb.org/daont#>
PREFIX dpv: <https://w3c.github.io/dpv/2.2/dpv/#>

SELECT DISTINCT 
    ?b2bSharing 
    ?holder 
    ?recipient 
    ?data
    ("UNJUSTIFIED_REFUSAL" AS ?violationType)
    ("Article 8(6) VIOLATED: Data holder refused to share data without trade secret justification" AS ?details)
WHERE {
  # B2B sharing agreement with authorization
  ?b2bSharing a daont:B2BDataSharing ;
              daont:governedBy ?contract ;
              daont:authorizedBy ?user .
  
  # User owns/uses a product
  ?user a daont:User ;
        daont:ownsOrUses ?product .
  
  # Product generates data
  ?product a daont:ConnectedProduct ;
           daont:generatesData ?data .
  
  # Data holder has this data
  ?holder a daont:DataHolder ;
          dpv:hasData ?data .
  
  # Contract has recipient
  ?contract dpv:hasRecipient ?recipient .
  ?recipient a daont:DataRecipient .
  
  # VIOLATION: No provide data action exists
  FILTER NOT EXISTS {
    ?holder daont:performsLegalAction ?provision .
    ?provision a daont:ProvideData .
  }
  
  # No trade secret justification exists
  FILTER NOT EXISTS {
    ?data daont:containsTradeSecret ?secret .
    ?secret a daont:TradeSecretData .
  }
}
ORDER BY ?recipient




#### OUTPUTS:

# b2bSharing || holder || recipient data || violationType || details
# http://www.semanticweb.org/daont#agreement247 || http://www.semanticweb.org/daont#RobotManufacturer || http://www.semanticweb.org/daont#autoRepair || http://www.semanticweb.org/daont#RobotData || "UNJUSTIFIED_REFUSAL" || "Article 8(6) VIOLATED: Data holder refused to share data without trade secret justification"